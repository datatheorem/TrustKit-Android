
apply plugin: 'com.android.library'

ext {
    userorg = ""
    bintrayRepo = 'TrustKit-Android'
    bintrayName = 'trustkit'

    publishedGroupId = 'com.datatheorem.android.trustkit'
    libraryName = 'TrustKit-Android'
    artifact = 'trustkit'

    libraryDescription = 'TrustKit Android is an open source library that makes it easy to deploy SSL public key ' +
            'pinning and reporting in any Android App.'
    gitUrl = 'https://github.com/datatheorem/TrustKit-Android'
    websiteUrl = 'https://github.com/datatheorem/TrustKit-Android'

    libraryVersion = trustkitVersionName

    licenseName = 'The MIT License (MIT)'
    licenseUrl = 'https://github.com/datatheorem/TrustKit-Android/blob/master/LICENSE'
    allLicenses = ["MIT"]
}

repositories {
    mavenCentral()
    maven {
        url "https://maven.mozilla.org/maven2"
    }
}

dependencies {
    implementation "com.android.support:support-annotations:$rootProject.libVersions.android.appCompat"
    implementation "com.android.support:support-v4:$rootProject.libVersions.android.appCompat"
    implementation "org.mozilla.components:lib-publicsuffixlist:$rootProject.libVersions.publicsuffixlist"

    androidTestImplementation "junit:junit:$rootProject.libVersions.junit"
    androidTestImplementation "com.android.support.test:runner:$rootProject.libVersions.android.testRunner"
    androidTestImplementation "com.android.support.test:rules:$rootProject.libVersions.android.testRunner"
    androidTestImplementation "org.mockito:mockito-core:$rootProject.libVersions.mockito.android"
    androidTestImplementation "org.awaitility:awaitility:3.1.6"
    androidTestImplementation "com.crittercism.dexmaker:dexmaker:$rootProject.libVersions.dexmaker"
    androidTestImplementation "com.crittercism.dexmaker:dexmaker-dx:$rootProject.libVersions.dexmaker"
    androidTestImplementation "com.crittercism.dexmaker:dexmaker-mockito:$rootProject.libVersions.dexmaker"
    androidTestImplementation "com.squareup.okhttp3:okhttp:$rootProject.libVersions.testing.okhttp3"
    androidTestImplementation "com.google.android.gms:play-services-base:$rootProject.libVersions.testing.playServicesBase"
}

android {
    defaultConfig {
        compileSdkVersion toolVersions.android.compileSdk
        buildToolsVersion toolVersions.android.buildTools
        minSdkVersion toolVersions.android.minSdk
        versionCode trustkitVersionCode
        versionName trustkitVersionName
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    }
}


apply plugin: 'com.github.dcendents.android-maven'
group = publishedGroupId

install {
    repositories.mavenInstaller {
        pom {
            project {
                packaging 'aar'
                groupId publishedGroupId
                artifactId artifact

                name libraryName
                description libraryDescription
                url websiteUrl

                licenses {
                    license {
                        name licenseName
                        url licenseUrl
                    }
                }

                developers {
                    developer {
                        id 'nabla-c0d3'
                        name 'Alban Diquet'
                        email 'ad@datatheorem.com'
                    }

                    developer {
                        id 'jobot0'
                        name 'Jordan Bouellat'
                        email 'jb@datatheorem.com'
                    }
                }

                scm {
                    connection gitUrl
                    developerConnection gitUrl
                    url websiteUrl
                }
            }
        }
    }
}

apply plugin: 'com.jfrog.bintray'

version = libraryVersion
// Create artifacts from the source
task sourcesJar(type: Jar) {
    classifier = 'sources'
    from android.sourceSets.main.java.srcDirs
}

artifacts {
    archives sourcesJar
}

bintray {
        user = System.getenv('BINTRAY_USER')
        key = System.getenv('BINTRAY_KEY')
        userorg = System.getenv('BINTRAY_USERORG')

    configurations = ['archives']
    pkg {
        repo = bintrayRepo
        name = bintrayName
        userOrg = userorg
        desc = libraryDescription
        websiteUrl = websiteUrl
        vcsUrl = gitUrl
        licenses = allLicenses
        publish = true
        version {
            name = libraryVersion
            desc = libraryDescription
        }
    }
}

